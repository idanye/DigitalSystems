// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //Determines whether the instuction is an A instruction
    Not(in=instruction[15], out=AIns); //A instruction
    Not(in=AIns, out=CIns); //Opposite of A instruction == C instruction

    //Determines whether the instruction is an C instruction
    And(a=CIns, b=instruction[5], out=ALUtoA); //C instuction

    //Selects which value, the A-instruction value or the ALU output, should go to the A register
    Mux16(a=instruction, b=ALUout, sel=ALUtoA, out=Aregin); //Stores the value the A-register

    //If A instruction or ALU output to A-register, load A
    Or(a=AIns, b=ALUtoA, out=loadA);
    ARegister(in=Aregin, load=loadA, out=Aout); //Generates the value inside the A-register

    //Selects A or M depending on the 12-bit instuction value
    Mux16(a=Aout, b=inM, sel=instruction[12], out=AMout);

    //Decides whether the ALU output is loaded to the D-register
    And(a=CIns, b=instruction[4], out=loadD);
    DRegister(in=ALUout, load=loadD, out=Dout); //loads the value of the D-register from the ALU

    //Calculates computations
    ALU(x=Dout, y=AMout, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUout, zr=ZRout, ng=NGout);

    //Sets the ALU outputs
    //Decides whether the CPU can write on the data memory
    Or16(a=false, b=Aout, out[0..14]=addressM); //the address of write operations
    Or16(a=false, b=ALUout, out=outM); //the content in the data memory
    And(a=CIns, b=instruction[3], out=writeM); //Write to Data memory
    And(a=ZRout, b=instruction[1], out=jeq); //is zero; jump
    And(a=NGout, b=instruction[2], out=jlt); //is negative; jump
    Or(a=ZRout, b=NGout, out=zeroOrNeg); //zero OR negative
    Not(in=zeroOrNeg, out=positive); //is positive
    And(a=positive, b=instruction[0], out=jgt);
    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=jumpToA);
    And(a=CIns, b=jumpToA, out=PCload); //load PC if C-instruction and conditions met
    Not(in=PCload, out=PCinc); //increment only if the PC is not loaded

    PC(in=Aout, load=PCload, inc=PCinc, reset=reset, out[0..14]=pc);
}